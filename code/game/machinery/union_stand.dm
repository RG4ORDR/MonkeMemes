/obj/machinery/union_stand
	name = "\improper holographic union stand"
	desc = "A holographic stand that allows Union members to vote on new demands. Reads badges for access."
	icon = 'monkestation/icons/obj/structures/signboards.dmi'
	icon_state = "holographic_sign"
	base_icon_state = "holographic_sign"

	req_one_access = list(ACCESS_UNION, ACCESS_UNION_LEADER)
	density = TRUE
	use_power = IDLE_POWER_USE
	idle_power_usage = BASE_MACHINE_IDLE_CONSUMPTION * 0.1
	active_power_usage = BASE_MACHINE_ACTIVE_CONSUMPTION
	circuit = /obj/item/circuitboard/machine/union_stand

	maptext_y = 32
	maptext_x = -16
	maptext_width = 64
	maptext_height = 64

	///What union we're a union stand for.
	var/datum/union/union_stand_for
	/// Holder for signboard maptext
	var/obj/effect/abstract/signboard_holder/text_holder

/obj/machinery/union_stand/Initialize(mapload)
	. = ..()
	union_stand_for = GLOB.cargo_union
	text_holder = new(src)
	vis_contents += text_holder

/obj/machinery/union_stand/Destroy()
	union_stand_for = null
	vis_contents -= text_holder
	QDEL_NULL(text_holder)
	return ..()

/obj/machinery/union_stand/update_icon_state()
	icon_state = base_icon_state
	if(machine_stat & NOPOWER)
		icon_state += "_blank"
	return ..()

/obj/machinery/union_stand/update_appearance(updates)
	. = ..()
	if(!union_stand_for.voting_timer)
		text_holder.maptext = null
		return
	var/bwidth = src.bound_width || world.icon_size
	var/bheight = src.bound_height || world.icon_size
	var/text_html = MAPTEXT_GRAND9K("<span style='text-align: center; line-height: 1'>VOTE FOR: [union_stand_for.demand_voting_on.name]</span>")
	SET_PLANE_EXPLICIT(text_holder, GAME_PLANE_UPPER_FOV_HIDDEN, src)
	text_holder.layer = ABOVE_ALL_MOB_LAYER
	text_holder.alpha = 192
	text_holder.maptext = text_html
	text_holder.maptext_x = (SIGNBOARD_WIDTH - bwidth) * -0.5
	text_holder.maptext_y = bheight
	text_holder.maptext_width = SIGNBOARD_WIDTH
	text_holder.maptext_height = SIGNBOARD_HEIGHT

/obj/machinery/union_stand/on_changed_z_level(turf/old_turf, turf/new_turf, same_z_layer, notify_contents)
	if(!same_z_layer)
		SET_PLANE_EXPLICIT(text_holder, GAME_PLANE_UPPER_FOV_HIDDEN, src)
	return ..()

/obj/machinery/union_stand/ui_interact(mob/user, datum/tgui/ui)
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "UnionStand")
		ui.open()

/obj/machinery/union_stand/ui_data(mob/user)
	return union_stand_for.ui_data(user)

/obj/machinery/union_stand/ui_static_data(mob/user)
	return union_stand_for.ui_static_data(user)

/obj/machinery/union_stand/ui_act(action, list/params, datum/tgui/ui, datum/ui_state/state)
	. = ..()
	return union_stand_for.ui_act(action, params, ui, state)
